use (
  alias://expected
  {interceptor, method} = alias://doubles
  dep://axios
  dep://path
  dep://os
  {HttpConnector} = ~
)

suite(__filename, proc()
  const (
    host = "skynet.apm.akromio.com"
    base = "/"
  )

  fn createConnector(client) = HttpConnector(client=, host, base)

  suite("getItem()", proc()
    test("when item unexists, nil must be returned", async proc()
      # (1) act
      const (
        client = interceptor(
          axios
          get = method.returns(status = 404)
        )
        conn = createConnector(client)
        out = await(conn.getItem("/unknown"))
      )

      # (2) assessment
      expected(out).toBeNil()
    end)

    test("when item exists, value and content-type must be returned", async proc()
      # (1) act
      const (
        client = interceptor(
          axios
          get = method(
            returns = {
              status = 200
              headers = {'content-type' = "text/yaml"}
              data = $"spec: v1.0${os.EOL}cty: yaml"
            }
          )
        )
        conn = createConnector(client)
        out = await(conn.getItem("/jobs.yaml"))
      )

      # (2) assessment
      expected(out).toBeMap().equalTo(
        name = "/jobs.yaml"
        cty = "text/yaml"
        value = $"spec: v1.0${os.EOL}cty: yaml"
      )
    end)
  end)
end)