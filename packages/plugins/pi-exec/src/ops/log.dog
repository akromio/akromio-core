use (
  {spawn} = dep://child_process
  buildParams = _buildParams
  buildTitle = _buildTitle
  buildOpts = _buildOpts
)

export {
  desc = "Runs a command, logging the output."
  title = buildTitle
  parameterizer = buildParams
  fun = handle
}

/**
 * Runs the exec command with log.
 */
@async
fn handle(ctx=> {params, log})
  # (1) prepare command
  var (
    cmd
    args
  )

  if params.command is list then
    cmd = params.command[0]
    args = params.command[1, -1]
  else
    const segments = params.command.split(re(" +"))
    cmd = segments[0]
    args = segments[1, -1]

  # (2) run
  const opts = buildOpts(params.opts)

  return promise(proc(resolve, reject)
    const ps = spawn(cmd, args, opts)

    ps.on("error", proc(error)
      log(error)
      reject(1)
    end)

    ps.stdout.on("data", proc(out)
      log(out)
    end)

    ps.stderr.on("data", proc(out)
      log(out)
    end)

    ps.on("exit", proc(code)
      if code == 0 then resolve(code)
      else reject(code)
    end)
  end)
