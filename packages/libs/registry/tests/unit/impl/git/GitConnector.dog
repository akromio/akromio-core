use (
  alias://expected
  {interceptor, method} = alias://doubles
  dep://axios
  dep://path
  dep://os
  {GitConnector} = ~
)

suite(__filename, proc()
  const (
    user = "akromio"
    repo = "test"
    branch = "master"
    prefix = ""
  )

  fn createConnector(client) = GitConnector(client=, user, repo, branch, prefix)

  suite("getItem()", proc()
    test("when item unexists, nil must be returned", async proc()
      # (1) act
      const (
        client = interceptor(
          axios
          get = method.returns(status = 204)
        )
        conn = createConnector(client)
        out = await(conn.getItem("/unknown"))
      )

      # (2) assessment
      expected(out).toBeNil()
    end)

    test("when item is a yaml file, value and mime must be returned", async proc()
      # (1) act
      const (
        client = interceptor(
          axios
          get = method(
            returns = {
              status = 200
              headers = {'content-type' = "text/plain"}
              data = $"spec: v1.0${os.EOL}cty: yaml"
            }
          )
        )
        conn = createConnector(client)
        itemPath = "/jobs.yaml"
        out = await(conn.getItem(itemPath))
      )

      # (2) assessment
      expected(out).toBeMap().toHave(
        cty = "text/yaml"
        value = $"spec: v1.0${os.EOL}cty: yaml"
      )

      expected.path(out.name).equalTo(itemPath)
    end)

    test("when item w/o extension, value and content-type must be returned", async proc()
      # (1) act
      const (
        client = interceptor(
          axios
          get = method(
            returns = {
              status = 200
              headers = {'content-type' = "text/plain"}
              data = "FROM golang:1.17-alpine as build"
            }
          )
        )
        conn = createConnector(client)
        itemPath = "/Dockerfile"
        out = await(conn.getItem(itemPath))
      )

      # (2) assessment
      expected(out).toBeMap().toHave(
        cty = "text/plain"
        value = "FROM golang:1.17-alpine as build"
      )

      expected.path(out.name).equalTo(itemPath)
    end)
  end)
end)
