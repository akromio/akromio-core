use (
  dep://axios
  alias://mime
  ../InternalConnector
)

/**
 * A registry connector to download files from
 * a Git repository.
 */
@impl(Connector)
export struct GitConnector: InternalConnector
  /**
   * HTTP client to use, compatible with Axios.
   */
  pub const client = axios

  /**
   * Host.
   */
  pub const host := "raw.githubusercontent.com"

  /**
   * The user account name where the repository is.
   */
  pub const user: text

  /**
   * Repository name.
   */
  pub const repo: text

  /**
   * Branch name.
   */
  pub const branch: text

  /**
   * Path prefix such as, for example, .akromio.
   */
  pub const prefix: text

  /**
   * @workaround
   * When axios gets a content in JSON format, its value is transformed to a map.
   * We need the text, not the map. For this reason, we disable the auto-transformation.
   */
  @override
  pub async fn _getItem(itemPath: text) -> item
    const (
      axios = self.client
      url = $"https://${self.host}/${self.user}/${self.repo}/${self.branch}/${self.prefix}${itemPath}"
      opts = {
        transformResponse = fn(resp) = resp end
      }
    )

    if [ok, resp] ::= pawait(axios.get(url, opts)); ok and resp.status == 200 then
      const (
        cty = mime.lookup(itemPath) or resp.headers["content-type"]
        value = resp.data
      )

      item = {name = itemPath, value, cty}
