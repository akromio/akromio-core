use (
  alias://expected
  {sim, monitor} = alias://doubles

  ../../../starters/BlankSheetStream
  ../../../ring/Ring
  ../../RunReqStream
  RandomAssigner
)

suite(__filename, proc()
  const (
    registry = "fs:///my/registry"
    catalog = "catalog-name"
    job = "job-name"
    ring = Ring(points = ["one", "two", "three"])
  )

  suite("constructor", proc()
    test("when total weight is not 100, error must be raised", proc()
      # (1) act
      const (
        assignations = [{registry, catalog, job, weight = 90}, {registry, catalog, job, weight = 20}]
        input = BlankSheetStream()
        output = RunReqStream()
        opts = {input, output, assignations, ring}
        out = peval(RandomAssigner(opts))
      )

      # (2) assessment
      expected(out).it(0).equalTo(false).it(1).equalTo(TypeError("Sum of assignation weights must be 100. Got: 110."))
    end)
  end)

  suite("start()", proc()
    test("when started, run requests must be generated", async proc()
      # (1) act
      const (
        blankSheets = text(timestamp().valueOf() + " ").repeat(100).split(" ")[0, -2].map(fn(ts) = {ts = num(ts)} end)
        assignations = [
          {registry, catalog, job = "#1", weight = 25},
          {registry, catalog, job = "#2", weight = 75}
        ]
        input = sim.stream.readable(objectMode = true, data = blankSheets)
        output = monitor(RunReqStream(), method = "append")
        opts = {input, output, assignations, ring}
        assigner = RandomAssigner(opts)
      )

      assigner.start()
      await(sleep("1500ms"))

      # (2) assessment
      expected(input.readable).equalTo(false)
      expected(output.writable).equalTo(false)

      var (
        log ::= monitor.log(output, clear = true)
        job1 = 0
        job2 = 0
        assignee1 = 0
        assignee2 = 0
        assignee3 = 0
      )

      expected(log.calls).equalTo(100)
      
      for i = 0; i < log.calls; i += 1 do
        const req = log.getCall(i).args[0]
        
        with req.job do
          if "#1" then job1 += 1
          if "#2" then job2 += 1

        with req.assignee do
          if "one" then assignee1 += 1
          if "two" then assignee2 += 1
          if "three" then assignee3 += 1
      
      expected(job1).equalTo(25)
      expected(job2).equalTo(75)
      expected(assignee1).greaterThanOrEqualTo(33)
      expected(assignee2).greaterThanOrEqualTo(33)
      expected(assignee3).greaterThanOrEqualTo(33)
    end)
  end)
end)
