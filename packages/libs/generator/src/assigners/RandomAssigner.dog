use (
  dep://array-shuffle
  Assigner
)

/**
 * An assigner to assign the jobs randomly.
 */
export struct RandomAssigner: Assigner
  /**
   * Current assignation order.
   */
  @hidden @strict
  pub var assignationOrder = []

  /**
   * @dev
   * The job weights must sum 100.
   */
  @post
  pvt proc post()
    # (1) calculate the total weight
    var total = 0

    for each assign in self.assignations do
      total += assign.weight
    
    # (2) check total weight
    if total != 100 then
      throw(TypeError($"Sum of assignation weights must be 100. Got: ${total}."))

  /**
   * Starts the assigner, reading from the input stream, assigning
   * and writing to the output stream.
   *
   * When the input stream is ended, this assigner ends too.
   */
  @override @noParamCheck
  pub fn assign(blankSheet: BlankSheet): RunReq
    # (1) generate a new assignation batch if needed
    if len(self.assignationOrder) == 0 then
      self.assignationOrder = self.generateAssignationOrder()
    
    # (2) determine assignation
    const assignation = self.assignationOrder.pop()
    return assignation{*, -weight, ...blankSheet, assignTs = timestamp().valueOf()}

  /**
   * Generates an assignation order for 100 assignations,
   * attending to the weights.
   */
  @hidden
  pub fn generateAssignationOrder() -> order: list
    # (1) generate order
    for each assign in shuffle(self.assignations) do
      for i = 0; i < assign.weight; i += 1 do
        order.push(assign)

    # (2) perform final shuffle
    order = shuffle(order)
