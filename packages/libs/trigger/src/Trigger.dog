use (
  {CallReq, CallReqStream} = alias://core
  TriggerImpl, TriggerState
)

/**
 * A trigger, that is, a component to request the execution
 * of a job.
 *
 * @remarks
 * This is a wrapper for the real triggers that are imported.
 */
@abstract
export struct Trigger
  /**
   * The trigger name.
   */
  pub const name: text

  /**
   * Stream where this trigger must let the job call requests.
   */
  pub const stream: CallReqStream

  /**
   * Function to call when finished.
   */
  pub var callback?: func

  /**
   * Current state.
   */
  @hidden @strict
  pub var _state = TriggerState.nonStarted

  /**
   * Current state.
   */
  pub fn state = self._state

  /**
   * Trigger implementation.
   */
  @hidden
  pub const triggerImpl: TriggerImpl

  /**
   * Starts the trigger.
   */
  pub async fn start(callback?: func) -> self
    # (1) pre
    if self.state !=~ nonStarted then
      throw(TypeError("The trigger has been already started."))

    # (2) start
    self.{callback}
    await(self.triggerImpl.start(bind(self, "handle")))
    self._state =~ started

  /**
   * Ends the trigger.
   */
  pub async fn stop() -> self
    if self.state not in [TriggerState.nonStarted, TriggerState.stopped] then
      self._state =~ stopped

      do
        await(self.triggerImpl.stop())
      finally
        if self.callback then self.callback()
  
  /**
   * Handles a given event.
   *
   * If the handler returns {finished = true}, the stop method
   * is called and the stream is set as ended.
   */
  @hidden
  pub async proc handle(e:> {call: CallReq, last?: bool})
    # (1) pre
    if self.state ==~ stopped then
      throw(TypeError("Trigger is stopped."))

    # (2) process event
    self._state =~ running

    if call.jobName == "__exit__" or last then
      self.stream.end()
      await(self.stop())
    else
      self.stream.appendCallReq(call)
      self._state =~ started
