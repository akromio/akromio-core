use (
  alias://expected

  CatalogItemBase = CatalogItem
  CatalogItemParseOpts
  CatalogItemParserBase = CatalogItemParser
)

# Test catalog item to use.
struct CatalogItem: CatalogItemBase
  pub const dataset?

# Test Ã¬tem parser to use.
struct CatalogItemParser: CatalogItemParserBase
  @override
  pub const itemName = "job"

  @override
  pub fn parseItem(decl: map, opts: CatalogItemParseOpts) = CatalogItem(decl{*, name = decl.macro})

suite(__filename, proc()
  const (
    parser = CatalogItemParser()
    parseOpts = {}
  )

  suite("parse()", proc()
    test("when called, a map with the item instances must be returned", proc()
      # (1) act
      const (
        decl = {macro = "test", steps = []}
        out = parser.parse(decl, parseOpts)
      )

      # (2) assessment
      expected(out.test).toBe(CatalogItem).toHave(name = "test", tags = [])
    end)
  end)

  suite("parseGroup()", proc()
    test("when group w/o dataset, its items must have the group name as tag", proc()
      # (1) act
      const (
        macro = {macro = "test", tags = [], steps = []}
        decl = {group = "grp", jobs = [macro]}
        out = parser.parse(decl, parseOpts)
      )

      # (2) assessment
      expected(out.test).toBe(CatalogItem).toHave(name = "test", tags = ["grp"])
    end)

    test("group w/ dataset, its items must have the group name as tag and the dataset", proc()
      # (1) act
      const (
        macro1 = {macro = "macro1", tags = [], steps = []}
        macro2 = {macro = "macro2", dataset = ["job"], tags = [], steps = []}
        decl = {group = "grp", dataset = ["group"], jobs = [macro1, macro2]}
        out = parser.parse(decl, parseOpts)
      )

      # (2) assessment
      expected(out.macro1).toBe(CatalogItem).toHave(
        name = "macro1"
        tags = ["grp"]
        dataset = ["group"]
      )

      expected(out.macro2).toBe(CatalogItem).toHave(
        name = "macro2"
        tags = ["grp"]
        dataset = ["group", "job"]
      )
    end)
  end)
end)
