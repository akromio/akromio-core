use (
  {Ops, MacroOperator, CoOperator, Script, ScriptOperator} = alias://core
  {CatalogMacro, CatalogCo, CatalogItemParser, CatalogItemParseOpts} = alias://catalog
)

/**
 * Parse options.
 */
intf ParseOpts: CatalogItemParseOpts
  /**
   * Ops registry to use with the cataloged ops such as,
   * for example, the macros.
   */
  ops: Ops

/**
 * A parser of job declarations.
 */
export struct JobParser: CatalogItemParser
  @override
  pub const itemName = "job"

  @override @hidden
  pub fn parseItem(decl: map, opts: ParseOpts) = self.parseJob(decl, opts)

  /**
   * Parses a job [decl]aration, returning its job instance.
   */
  pub fn parseJob(decl: map, opts: ParseOpts) -> job
    # (1) replace text item by map
    if local ::= decl.dataset; local is list then
      for i = 0; i < len(local); i += 1 do
        if datum ::= local[i]; datum is text then
          local[i] = {var = datum}
    
    # (2) parse job
    if "macro" in decl then job = self.parseMacro(decl, opts)
    else if "co" in decl then job = self.parseCo(decl, opts)
    else if "script" in decl then job = self.parseScript(decl, opts)
    else job = self.parseAddOnJob(decl, opts)
  
  /**
   * Parses an add-on job, that is, a specific job of the package in use.
   */
  @hidden
  pub fn parseAddOnJob(decl: map, opts: ParseOpts)
    throw(TypeError($"Invalid job declaration: ${fmt(decl)}."))
  
  /**
   * Parses a macro [decl]aration, using the given [opts].
   */
  @hidden
  pub fn parseMacro(decl: map, opts: ParseOpts) = CatalogMacro(
    decl{
      *
      name = decl.macro
      operator = MacroOperator()
      ops = opts.ops
      initializers = parseIni(decl)
      finalizers = parseFin(decl)
    }
  )

  /**
   * Parses a concurrent [decl]aration, using the given [opts].
   */
  @hidden
  pub fn parseCo(decl: map, opts: ParseOpts) = CatalogCo(
    decl{
      *
      name = decl.co
      operator = CoOperator()
      ops = opts.ops
    }
  )

  /**
   * Parses a script [decl]aration
   */
  @hidden
  pub fn parseScript(decl: map, _: ParseOpts) = Script(
    decl{
      *
      name = decl.script
      operator = ScriptOperator()
    }
  )

/**
 * Parses the ini field of a [decl]aration.
 *
 * @dev
 * This field must be a list, but only one inline step is accepted too.
 */
fn parseIni(decl: map) = if decl.ini is text then
  [decl.ini]
else
  decl.ini ?? []
end

/**
 * Parses the fin field of a [decl]aration.
 *
 * @dev
 * Similar to parseIni().
 */
fn parseFin(decl: map) = if decl.fin is text then
  [decl.fin]
else
  decl.fin ?? []
end
