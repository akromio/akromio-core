use (
  {Dataset} = alias://dataset
  {Ops, CatalogParser, CatalogParseOpts} = alias://catalog
  {Plugin, Plugins, PluginParser, PluginLoader} = alias://core

  ../JobCatalog
  JobParser
)

/**
 * The parser for the job declarations.
 */
const jobParser = JobParser()

/**
 * Parse options.
 */
intf JobCatalogParseOpts: CatalogParseOpts
  /**
   * The dataset parent.
   */
  parentDataset: Dataset

  /**
   * The ops registry to use with the cataloged ops such as, for example,
   * the macros.
   */
  ops: Ops

/**
 * A parser to parse jobs catalog declarations.
 */
export struct JobCatalogParser: CatalogParser
  /**
   * The plugin loader to use.
   */
  pub const pluginLoader: PluginLoader

  /**
   * The plugin parser to use.
   */
  pub const pluginParser: PluginParser

  @override @hidden
  pub fn createCatalog(decl: map) = JobCatalog(decl)

  @override @hidden
  pub async fn parseSpecific(decl: map, opts: JobCatalogParseOpts) -> decl
    # (1) parse base components
    const (
      {dataset} = decl
      triggers = self.parseTriggers(decl.on ?? [], dataset)
    )
  
    # (2) parse plugins
    const (
      {ops} = opts
      pluginsDecl = dataset.eval(decl.plugins ?? [])
      plugins = await(self.parsePlugins(pluginsDecl))
    )

    for each pi: Plugin in plugins.plugins do
      ops.appendPlugin(pi)

    # (3) parse jobs
    const jobs = self.parseJobs(decl.jobs, opts)

    # (4) adapt declaration
    decl.{plugins, jobs, triggers}

  /**
   * Parses the plugin declarations,
   * returning the instances for these declarations.
   */
  @hidden
  pub async fn parsePlugins(decl: map[]) -> plugins: Plugins
    const (
      loader = self.pluginLoader
      parser = self.pluginParser
    )

    plugins = Plugins()

    for each def in decl do
      if "preset" in def then
        # get preset declaration
        const psDecl = loader.loadPreset(def.preset)

        # create plugin instances from preset
        for each plugin: Plugin in await(parser.parsePreset(psDecl)) do
          plugins.appendPlugin(plugin)
      else if "plugin" in def then
        # get plugin declaration
        const piDecl = loader.loadPlugin(def.impl)

        # create new plugin instance
        const name = def.name ?? def.plugin
        plugins.appendPlugin(await(parser.parsePlugin(piDecl{*, plugin = name}, def.ini)))
      else
        throw(TypeError($"Unknown plugin declaration: ${fmt(def)}."))
  
  /**
   * Parses the trigger declarations,
   * returning a map with these declarations
   * with the data already evaluated.
   */
  @hidden
  pub fn parseTriggers(decl: map[], ds: Dataset) -> triggers: map
    for each trgDecl in ds.eval(decl) do
      triggers[trgDecl.trigger] = trgDecl
  
  /**
   * Parses the job declarations,
   * returning a list with the job instances.
   */
  @hidden
  pub fn parseJobs(decl: map[], opts: JobCatalogParseOpts) = jobParser.parse(
    decl
    opts
  )
